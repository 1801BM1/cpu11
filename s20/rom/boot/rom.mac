        .TITLE	CMROM -- DUMP CM1420 ROM
        .IDENT	/V01.00/

BASE	=:	173000			; ROM BASE ADDRESS
PAGE	=:	173024			; ROM PAGE NUMBER REGISTER

	.MCALL	.EXIT,.PRINT		; SYSTEM MACRO CALLS
					;
START::	CLR	R3			; INITIALIZE PAGE NUMBER
10$:	MOV	R3,@#PAGE		; SELECT ROM PAGE
	MOV	#BASE,R5		; SET BASE ADDRESS
20$:	MOV	#BUFF,R0		; R0=ADDRESS OF BUFFER
	MOV	R5,R1			; R1=ADDRESS
	MOV	PC,R2			; R2=LEADING ZEROS FLAG
	CALL	$CBOMG			; CONVERT ADDRESS TO ASCII
	MOV	#FMTS,R1		; R1=FORMAT STRING
	MOV	R5,R2			; R2=ADDRESS OF ARGUMENT BLOCK
	CALL	$EDMSG			; EDIT MESSAGE
	.PRINT	#BUFF			; PRINT IT
	ADD	#20,R5			; TO NEXT BLOCK
	CMP	#BASE+1000,R5		; END OF PAGE REACHED?
	BNE	20$			; IF NE NO
	.PRINT	#CRLF			; PRINT PAGE SEPARATOR
	ADD	#100,R3			; TO NEXT PAGE
	CMP	#400,R3			; ALL PAGES PRINTED?
	BNE	10$			; IF NE NO
	.EXIT				; EXIT

FMTS:	.ASCII	/  %P %P %P %P %P %P %P %P/
CRLF:	.BYTE	0
BUFF:	.BLKB	80.
        .END	START

	